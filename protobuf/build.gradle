plugins {
    id "java-library"
    id "maven-publish"

    id "com.google.protobuf"
    id "me.champeau.gradle.japicmp"
    id "ru.vyarus.animalsniffer"
}

description = 'gRPC: Protobuf'

dependencies {
    api project(':grpc-api'),
            libraries.jsr305,
            libraries.protobuf.java
    implementation libraries.guava

    api (libraries.google.api.protos) {
        // 'com.google.api:api-common' transitively depends on auto-value, which breaks our
        // annotations.
        exclude group: 'com.google.api', module: 'api-common'
    }

    api (project(':grpc-protobuf-lite')) {
        exclude group: 'com.google.protobuf', module: 'protobuf-javalite'
    }

    signature libraries.signature.java
    signature libraries.signature.android
}

tasks.named("javadoc").configure {
    options.links 'https://protobuf.dev/reference/java/api-docs/'
}
